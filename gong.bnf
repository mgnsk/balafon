/* Lexical Part */

terminator : ';' '\n' | ';' | '\n' ;

uint      : _uint ;
_uint        : '0' | '1'-'9' {'0'-'9'} ;

char      : 'a'-'z' | 'A'-'Z' ;
stringLit : '"' { . } '"' ;

rest     : '-' ;
sharp   : '#' ;
flat    : '$' ;
accent  : '^' ;
ghost   : ')' ;
dot     : '.' ;
tuplet  : '/' '3' | '/' '5' ;
letRing : '*' ;

lineComment  : '/' '/' { . } '\n' ;
blockComment : '/' '*' { . | '*' } '*' '/' ;

!whitespace : ' ' | '\t' | '\r' ;

/* Syntax Part */

<<
import (
    "fmt"
    "github.com/mgnsk/gong/internal/ast"
    "github.com/mgnsk/gong/internal/parser/token"
)
>>

SourceFile
        : RepeatTerminator TopLevelDeclList << $1, nil >>
        ;

RepeatTerminator
    : empty
    | terminator RepeatTerminator
    ;

TopLevelDeclList
    : TopLevelDecl terminator RepeatTerminator TopLevelDeclList << ast.NewDeclList($0.(fmt.Stringer), $3.(ast.DeclList)), nil >>
    | TopLevelDecl RepeatTerminator  << ast.NewDeclList($0.(fmt.Stringer), nil), nil >>
    ;

BarBodyDeclList
    : BarBodyDecl terminator RepeatTerminator BarBodyDeclList << ast.NewDeclList($0.(fmt.Stringer), $3.(ast.DeclList)), nil >>
    | BarBodyDecl RepeatTerminator  << ast.NewDeclList($0.(fmt.Stringer), nil), nil >>
    ;

TopLevelDecl
    : Bar
    | Comment
    | TopLevelCommand
    | NoteList
    ;

BarBodyDecl
    : Comment
    | BarBodyCommand
    | NoteList
    ;

Comment
    : lineComment << ast.NewLineComment($T0), nil >>
    | blockComment << ast.NewBlockComment($T0), nil >>
    ;

Bar
    : "bar" stringLit "{" RepeatTerminator BarBodyDeclList "}" << ast.NewBar($T1.StringValue(), $4.(ast.DeclList)), nil >>
    ;

NoteList
    : NoteObject << ast.NewNoteList($0.(fmt.Stringer), nil), nil >>
    | NoteObject NoteList << ast.NewNoteList($0.(fmt.Stringer), $1.(ast.NoteList)), nil >>
    ;

NoteObject
    : NoteSymbol PropertyList       << ast.NewNote($T0.IDValue(), $1.(ast.PropertyList)), nil >>
    | "[" NoteList "]" PropertyList << ast.NewNoteListFromGroup($1.(ast.NoteList), $3.(ast.PropertyList)) >>
    ;

NoteSymbol
    : char
    | rest
    ;

PropertyList
    : empty                         << ast.PropertyList(nil), nil >>
    | Property PropertyList         << ast.NewPropertyList($T0, $1) >>
    ;

Property
    : sharp
    | flat
    | accent
    | ghost
    | uint
    | dot
    | tuplet
    | letRing
    ;

TopLevelCommand
    : "assign" char uint            << ast.NewCmdAssign($T1, $T2) >>
    | BarBodyCommand
    ;

BarBodyCommand
    : "tempo" uint                  << ast.NewCmdTempo($T1) >>
    | "timesig" uint uint           << ast.NewCmdTimeSig($T1, $T2) >>
    | "channel" uint                << ast.NewCmdChannel($T1) >>
    | "velocity" uint               << ast.NewCmdVelocity($T1) >>
    | "program" uint                << ast.NewCmdProgram($T1) >>
    | "control" uint uint           << ast.NewCmdControl($T1, $T2) >>
    | "play" stringLit              << ast.CmdPlay($T1.StringValue()), nil >>
    | "start"                       << ast.CmdStart{}, nil >>
    | "stop"                        << ast.CmdStop{}, nil >>
    ;
