/* Lexical Part */

_uint        : '0' | '1'-'9' {'0'-'9'} ;
_lineComment : '/' '/' { . } '\n' ;

char       : 'a'-'z' | 'A'-'Z' ;
rest       : '-' ;
stringLit  : '"' { . } '"' ;
sharp      : '#' ;
flat       : '$' ;
accent     : '^' ;
ghost      : ')' ;
uint       : _uint ;
dot        : '.' ;
tuplet     : '/' _uint ;
letRing    : '*' ;

!comment    : _lineComment ;
!whitespace : ' ' | '\t' | '\n' | '\r' ;

/* Syntax Part */

<<
import (
    "github.com/mgnsk/gong/internal/ast"
    "github.com/mgnsk/gong/internal/parser/token"
)
>>

Expr
    : NoteList
    | Command
    ;

NoteList
    : empty
    | NoteGroup NoteList            << ast.NewNoteList($0, $1), nil >>
    | Note NoteList                 << ast.NewNoteList($0, $1), nil >>
    ;

NoteGroup
    : "[" NoteList "]" PropertyList << ast.NewNoteListFromGroup($1, $3) >>
    ;

Note
    : NoteSymbol PropertyList       << ast.NewNote($T0.IDValue(), $1), nil >>
    ;

NoteSymbol
    : char
    | rest
    ;

PropertyList
    : empty
    | Property PropertyList         << ast.NewPropertyList($T0, $1) >>
    ;

Property
    : sharp
    | flat
    | accent
    | ghost
    | uint                          << ast.NewNoteValue($T0) >>
    | dot
    | tuplet
    | letRing
    ;

Command:
    : CommandName ArgumentList      << ast.NewCommand($T0.IDValue(), $1) >>
    ;

ArgumentList:
    : empty
    | char ArgumentList             << ast.NewArgumentList($T0, $1), nil >>
    | stringLit ArgumentList        << ast.NewArgumentList($T0, $1), nil >>
    | uint ArgumentList             << ast.NewArgumentList($T0, $1), nil >>
    ;

CommandName:
    : "assign"
    | "tempo"
    | "channel"
    | "velocity"
    | "program"
    | "control"
    | "bar"
    | "end"
    | "play"
    | "start"
    | "stop"
    ;
