/* Lexical Part */

terminator : ';' '\n' | ';' | '\n' ;

uint      : _uint ;
_uint        : '0' | '1'-'9' {'0'-'9'} ;

char      : 'a'-'z' | 'A'-'Z' ;
stringLit : '"' { . } '"' ;

rest     : '-' ;
sharp   : '#' ;
flat    : '$' ;
accent  : '^' ;
ghost   : ')' ;
dot     : '.' ;
tuplet  : '/' '3' | '/' '5' ;
letRing : '*' ;

_lineComment  : '/' '/' { . } '\n' ;
_blockComment : '/' '*' { . | '*' } '*' '/' ;

!comment    : _lineComment | _blockComment ;
!whitespace : ' ' | '\t' | '\r' ;

/* Syntax Part */

<<
import (
    "github.com/mgnsk/gong/ast"
    "github.com/mgnsk/gong/internal/parser/token"
)
>>

SourceFile
    : RepeatTerminator TopLevelDeclList << $1, nil >>
    ;

RepeatTerminator
    : empty
    | terminator RepeatTerminator
    ;

TopLevelDeclList
    : TopLevelDecl terminator RepeatTerminator TopLevelDeclList << ast.NewNodeList($0.(ast.Node), $3.(ast.NodeList)), nil >>
    | TopLevelDecl RepeatTerminator  << ast.NewNodeList($0.(ast.Node), nil), nil >>
    ;

BarBodyDeclList
    : BarBodyDecl terminator RepeatTerminator BarBodyDeclList << ast.NewNodeList($0.(ast.Node), $3.(ast.NodeList)), nil >>
    | BarBodyDecl RepeatTerminator  << ast.NewNodeList($0.(ast.Node), nil), nil >>
    ;

TopLevelDecl
    : Bar
    | TopLevelCommand
    | NoteList
    ;

BarBodyDecl
    : BarBodyCommand
    | NoteList
    ;

Bar
    : "bar" stringLit RepeatTerminator BarBodyDeclList "end" << ast.NewBar($T1, $3.(ast.NodeList)), nil >>
    ;

NoteList
    : NoteObject << ast.NewNoteList($0.(ast.Node), nil), nil >>
    | NoteObject NoteList << ast.NewNoteList($0.(ast.Node), $1.(ast.NoteList)), nil >>
    ;

NoteObject
    : NoteSymbol PropertyList       << ast.NewNote($T0, $1.(ast.PropertyList)), nil >>
    | "[" NoteList "]" PropertyList << ast.NewNoteListFromGroup($1.(ast.NoteList), $3.(ast.PropertyList)) >>
    ;

NoteSymbol
    : char
    | rest
    ;

PropertyList
    : empty                         << ast.PropertyList(nil), nil >>
    | Property PropertyList         << ast.NewPropertyList($T0, $1) >>
    ;

Property
    : sharp
    | flat
    | accent
    | ghost
    | uint
    | dot
    | tuplet
    | letRing
    ;

TopLevelCommand
    : "assign" char uint            << ast.NewCmdAssign($T1, $T2) >>
    | "play" stringLit              << ast.NewCmdName($T1) >>
    | BarBodyCommand
    ;

BarBodyCommand
    : "tempo" uint                  << ast.NewCmdTempo($T1) >>
    | "timesig" uint uint           << ast.NewCmdTimeSig($T1, $T2) >>
    | "velocity" uint               << ast.NewCmdVelocity($T1) >>
    | "channel" uint                << ast.NewCmdChannel($T1) >>
    | "program" uint                << ast.NewCmdProgram($T1) >>
    | "control" uint uint           << ast.NewCmdControl($T1, $T2) >>
    | "start"                       << ast.CmdStart{}, nil >>
    | "stop"                        << ast.CmdStop{}, nil >>
    ;
